group 'ledgersoft'

if (System.getenv("TRAVIS_TAG") != null) {
    version = "${System.getenv("TRAVIS_TAG")}"
} else {
    version '0.5.0'
}

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

build.finalizedBy javadoc

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'ledger.user_interface.ui_controllers.Startup'
    }
    baseName = 'Ledger'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task prod(dependsOn: [clean, build, test, fatJar, myJavadocs]) {
    build.mustRunAfter clean
    fatJar.mustRunAfter test
    myJavadocs.mustRunAfter test
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    // provides org.testfx.api, org.testfx.matcher to the test source set.
    testCompile "org.testfx:testfx-core:4.0.+"
    // provides org.testfx.framework.junit to the test source set.
    testCompile "org.testfx:testfx-junit:4.0.+"
    testCompile 'org.testfx:openjfx-monocle:1.8.0_20'

    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
    compile group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    compile group: 'com.h2database', name: 'h2', version: '1.4.192'
    compile group: 'com.google.http-client', name: 'google-http-client', version: '1.22.0'
    compile group: 'com.google.http-client', name: 'google-http-client-jackson', version: '1.22.0'
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.12'
}